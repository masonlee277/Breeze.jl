#####
##### Boundary condition wrappers for thermodynamic variable conversions
#####
##### These functions allow users to specify boundary conditions in terms of one
##### thermodynamic variable (e.g., energy flux) and have them automatically converted
##### to the equivalent BC for another variable (e.g., potential temperature flux).
#####

#####
##### EnergyFluxBoundaryConditionFunction: converts energy flux → potential temperature flux
##### Used when: user specifies ρs BCs but prognostic variable is ρθ
#####

"""
    EnergyFluxBoundaryConditionFunction

A wrapper for boundary conditions that converts energy flux to potential temperature flux.

When using `LiquidIcePotentialTemperatureFormulation`, the prognostic thermodynamic variable
is ``ρθ`` (potential temperature density). This wrapper allows users to specify energy fluxes
(e.g., sensible heat flux in W/m²) which are converted to potential temperature fluxes by
using the local heat capacity, Exner function, and the implemented temperature coordinate.

The relationship is:
```math
Jᶿ = 𝒬 / (cᵖᵐ Π)
```

where ``𝒬`` is the energy flux and ``Jᶿ`` is the potential temperature density flux
for reference-pressure dynamics. Compressible dynamics additionally uses the dry/total
density ratio and fixed-phase temperature Jacobian, consistently with volumetric
radiative heating. Saturation-adjustment phase changes retain the legacy approximation.

The mixture heat capacity is computed using moisture fractions from the microphysics scheme,
which correctly accounts for liquid and ice condensate when present.
"""
struct EnergyFluxBoundaryConditionFunction{C, S, M, TC, D, R}
    condition :: C
    side :: S
    microphysics :: M
    thermodynamic_constants :: TC
    density :: D
    response :: R
end

EnergyFluxBoundaryConditionFunction(c, s, m, tc, d) =
    EnergyFluxBoundaryConditionFunction(c, s, m, tc, d, nothing)

function Adapt.adapt_structure(to, ef::EnergyFluxBoundaryConditionFunction)
    return EnergyFluxBoundaryConditionFunction(Adapt.adapt(to, ef.condition),
                                               Adapt.adapt(to, ef.side),
                                               Adapt.adapt(to, ef.microphysics),
                                               Adapt.adapt(to, ef.thermodynamic_constants),
                                               Adapt.adapt(to, ef.density),
                                               Adapt.adapt(to, ef.response))
end

function Base.summary(ef::EnergyFluxBoundaryConditionFunction)
    cond = ef.condition
    cond_str = cond isa Number ? string(cond) : summary(cond)
    return string("EnergyFluxBoundaryConditionFunction(", cond_str, ")")
end

# Type aliases for dispatch on boundary side
const BottomEnergyFluxBC = EnergyFluxBoundaryConditionFunction{<:Any, <:Bottom}
const TopEnergyFluxBC    = EnergyFluxBoundaryConditionFunction{<:Any, <:Top}
const WestEnergyFluxBC   = EnergyFluxBoundaryConditionFunction{<:Any, <:West}
const EastEnergyFluxBC   = EnergyFluxBoundaryConditionFunction{<:Any, <:East}
const SouthEnergyFluxBC  = EnergyFluxBoundaryConditionFunction{<:Any, <:South}
const NorthEnergyFluxBC  = EnergyFluxBoundaryConditionFunction{<:Any, <:North}

# Near-surface density for the energy→θ flux conversion. The energy BC materializes
# *before* the dynamics allocates its density (issue #777): for `AnelasticDynamics`
# the reference density already exists and is captured here, but for
# `CompressibleDynamics` the captured `ef.density` is `nothing`. Resolve it at runtime
# instead — fall back to the prognostic density `ρ` carried in the model `fields`
# (`dynamics_prognostic_fields(::CompressibleDynamics) = (; ρᵈ=dynamics.dry_density)`).
# Type-stable: `ef.density` is a `Field` (anelastic) or `Nothing` (compressible) per BC.
@inline _energy_flux_density(density, fields, i, j, k) = @inbounds density[i, j, k]
@inline _energy_flux_density(::Nothing, fields, i, j, k) = @inbounds fields.ρᵈ[i, j, k]

# Convert physical energy flux using the materialized temperature coordinate.
@inline 𝒬_to_Jᶿ(i, j, k, grid, ef, 𝒬, fields) =
    energy_flux_to_theta(ef.response, i, j, k, grid, ef, 𝒬, fields)

@inline energy_flux_to_theta(response, i, j, k, grid, ef, 𝒬, fields) =
    response(i, j, k, grid, ef, 𝒬, fields)

@inline function energy_flux_to_theta(::Nothing, i, j, k, grid, ef, 𝒬, fields)
    qᵛ = @inbounds fields.qᵛ[i, j, k]
    ρ = _energy_flux_density(ef.density, fields, i, j, k)
    q = grid_moisture_fractions(i, j, k, grid, ef.microphysics, ρ, qᵛ, fields)
    cᵖᵐ = mixture_heat_capacity(q, ef.thermodynamic_constants)
    return 𝒬 / cᵖᵐ
end

# getbc for bottom boundary (k = 1)
@inline function OceananigansBC.getbc(ef::BottomEnergyFluxBC, i::Integer, j::Integer,
                                      grid::AbstractGrid, clock, fields)
    𝒬 = OceananigansBC.getbc(ef.condition, i, j, grid, clock, fields)
    return 𝒬_to_Jᶿ(i, j, 1, grid, ef, 𝒬, fields)
end

# getbc for top boundary (k = Nz)
@inline function OceananigansBC.getbc(ef::TopEnergyFluxBC, i::Integer, j::Integer,
                                      grid::AbstractGrid, clock, fields)
    𝒬 = OceananigansBC.getbc(ef.condition, i, j, grid, clock, fields)
    return 𝒬_to_Jᶿ(i, j, grid.Nz, grid, ef, 𝒬, fields)
end

# getbc for west boundary (i = 1)
@inline function OceananigansBC.getbc(ef::WestEnergyFluxBC, j::Integer, k::Integer,
                                      grid::AbstractGrid, clock, fields)
    𝒬 = OceananigansBC.getbc(ef.condition, j, k, grid, clock, fields)
    return 𝒬_to_Jᶿ(1, j, k, grid, ef, 𝒬, fields)
end

# getbc for east boundary (i = Nx)
@inline function OceananigansBC.getbc(ef::EastEnergyFluxBC, j::Integer, k::Integer,
                                      grid::AbstractGrid, clock, fields)
    𝒬 = OceananigansBC.getbc(ef.condition, j, k, grid, clock, fields)
    return 𝒬_to_Jᶿ(grid.Nx, j, k, grid, ef, 𝒬, fields)
end

# getbc for south boundary (j = 1)
@inline function OceananigansBC.getbc(ef::SouthEnergyFluxBC, i::Integer, k::Integer,
                                      grid::AbstractGrid, clock, fields)
    𝒬 = OceananigansBC.getbc(ef.condition, i, k, grid, clock, fields)
    return 𝒬_to_Jᶿ(i, 1, k, grid, ef, 𝒬, fields)
end

# getbc for north boundary (j = Ny)
@inline function OceananigansBC.getbc(ef::NorthEnergyFluxBC, i::Integer, k::Integer,
                                      grid::AbstractGrid, clock, fields)
    𝒬 = OceananigansBC.getbc(ef.condition, i, k, grid, clock, fields)
    return 𝒬_to_Jᶿ(i, grid.Ny, k, grid, ef, 𝒬, fields)
end

const EnergyFluxBCType = BoundaryCondition{<:Flux, <:EnergyFluxBoundaryConditionFunction}

"""
    EnergyFluxBoundaryCondition(flux)

Create a boundary condition that wraps an energy flux and converts it to a potential
temperature flux for use with `LiquidIcePotentialTemperatureFormulation`.

The physical energy flux is converted using the formulation and dynamics of the
materialized model; reference-pressure dynamics uses ``Jᶿ = 𝒬 / (cᵖᵐ Π)``.
Compressible fixed-phase states use the implemented temperature Jacobian.
"""
function EnergyFluxBoundaryCondition(flux)
    ef = EnergyFluxBoundaryConditionFunction(flux, nothing, nothing, nothing, nothing)
    return BoundaryCondition(Flux(), ef)
end

#####
##### ThetaFluxBoundaryConditionFunction: converts potential temperature flux → energy flux
##### Used when: user wants energy flux diagnostics but prognostic variable is ρθ
#####

"""
    ThetaFluxBoundaryConditionFunction

A wrapper for boundary conditions that converts potential temperature flux to energy flux.

When building a diagnostic `energy_density` field from a `PotentialTemperatureFormulation`,
the boundary conditions on `ρθ` (potential temperature density) must be converted to
energy flux boundary conditions using the inverse of the physical energy-to-theta
conversion for the same dynamics and temperature solver.

The relationship is:
```math
𝒬 = Jᶿ cᵖᵐ Π
```

where ``𝒬`` is the energy flux and ``Jᶿ`` is the potential temperature density flux
for reference-pressure dynamics. Compressible dynamics uses the inverse of its
fixed-phase energy-to-theta response.
"""
struct ThetaFluxBoundaryConditionFunction{C, S, M, TC, D, R}
    condition :: C
    side :: S
    microphysics :: M
    thermodynamic_constants :: TC
    density :: D
    response :: R
end

ThetaFluxBoundaryConditionFunction(c, s, m, tc, d) =
    ThetaFluxBoundaryConditionFunction(c, s, m, tc, d, nothing)

ThetaFluxBoundaryConditionFunction(condition, side, microphysics, thermodynamic_constants) =
    ThetaFluxBoundaryConditionFunction(condition, side, microphysics, thermodynamic_constants, nothing)

function Adapt.adapt_structure(to, tf::ThetaFluxBoundaryConditionFunction)
    return ThetaFluxBoundaryConditionFunction(Adapt.adapt(to, tf.condition),
                                              Adapt.adapt(to, tf.side),
                                              Adapt.adapt(to, tf.microphysics),
                                              Adapt.adapt(to, tf.thermodynamic_constants),
                                              Adapt.adapt(to, tf.density),
                                              Adapt.adapt(to, tf.response))
end

function Base.summary(tf::ThetaFluxBoundaryConditionFunction)
    cond = tf.condition
    cond_str = cond isa Number ? string(cond) : summary(cond)
    return string("ThetaFluxBoundaryConditionFunction(", cond_str, ")")
end

# Type aliases for dispatch on boundary side
const BottomThetaFluxBC = ThetaFluxBoundaryConditionFunction{<:Any, <:Bottom}
const TopThetaFluxBC    = ThetaFluxBoundaryConditionFunction{<:Any, <:Top}
const WestThetaFluxBC   = ThetaFluxBoundaryConditionFunction{<:Any, <:West}
const EastThetaFluxBC   = ThetaFluxBoundaryConditionFunction{<:Any, <:East}
const SouthThetaFluxBC  = ThetaFluxBoundaryConditionFunction{<:Any, <:South}
const NorthThetaFluxBC  = ThetaFluxBoundaryConditionFunction{<:Any, <:North}

# Invert the same physical energy-to-theta response used by the model.
@inline Jᶿ_to_𝒬(i, j, k, grid, tf, Jᶿ, fields) =
    theta_flux_to_energy(tf.response, i, j, k, grid, tf, Jᶿ, fields)

@inline theta_flux_to_energy(response, i, j, k, grid, tf, Jᶿ, fields) =
    Jᶿ / energy_flux_to_theta(response, i, j, k, grid, tf, one(Jᶿ), fields)

@inline function theta_flux_to_energy(::Nothing, i, j, k, grid, tf, Jᶿ, fields)
    qᵛ = @inbounds fields.qᵛ[i, j, k]
    ρ = @inbounds tf.density[i, j, k]
    q = grid_moisture_fractions(i, j, k, grid, tf.microphysics, ρ, qᵛ, fields)
    cᵖᵐ = mixture_heat_capacity(q, tf.thermodynamic_constants)
    return Jᶿ * cᵖᵐ
end

# getbc for bottom boundary (k = 1)
@inline function OceananigansBC.getbc(tf::BottomThetaFluxBC, i::Integer, j::Integer,
                                      grid::AbstractGrid, clock, fields)
    Jᶿ = OceananigansBC.getbc(tf.condition, i, j, grid, clock, fields)
    return Jᶿ_to_𝒬(i, j, 1, grid, tf, Jᶿ, fields)
end

# getbc for top boundary (k = Nz)
@inline function OceananigansBC.getbc(tf::TopThetaFluxBC, i::Integer, j::Integer,
                                      grid::AbstractGrid, clock, fields)
    Jᶿ = OceananigansBC.getbc(tf.condition, i, j, grid, clock, fields)
    return Jᶿ_to_𝒬(i, j, grid.Nz, grid, tf, Jᶿ, fields)
end

# getbc for west boundary (i = 1)
@inline function OceananigansBC.getbc(tf::WestThetaFluxBC, j::Integer, k::Integer,
                                      grid::AbstractGrid, clock, fields)
    Jᶿ = OceananigansBC.getbc(tf.condition, j, k, grid, clock, fields)
    return Jᶿ_to_𝒬(1, j, k, grid, tf, Jᶿ, fields)
end

# getbc for east boundary (i = Nx)
@inline function OceananigansBC.getbc(tf::EastThetaFluxBC, j::Integer, k::Integer,
                                      grid::AbstractGrid, clock, fields)
    Jᶿ = OceananigansBC.getbc(tf.condition, j, k, grid, clock, fields)
    return Jᶿ_to_𝒬(grid.Nx, j, k, grid, tf, Jᶿ, fields)
end

# getbc for south boundary (j = 1)
@inline function OceananigansBC.getbc(tf::SouthThetaFluxBC, i::Integer, k::Integer,
                                      grid::AbstractGrid, clock, fields)
    Jᶿ = OceananigansBC.getbc(tf.condition, i, k, grid, clock, fields)
    return Jᶿ_to_𝒬(i, 1, k, grid, tf, Jᶿ, fields)
end

# getbc for north boundary (j = Ny)
@inline function OceananigansBC.getbc(tf::NorthThetaFluxBC, i::Integer, k::Integer,
                                      grid::AbstractGrid, clock, fields)
    Jᶿ = OceananigansBC.getbc(tf.condition, i, k, grid, clock, fields)
    return Jᶿ_to_𝒬(i, grid.Ny, k, grid, tf, Jᶿ, fields)
end

const ThetaFluxBCType = BoundaryCondition{<:Flux, <:ThetaFluxBoundaryConditionFunction}

"""
    ThetaFluxBoundaryCondition(flux)

Create a boundary condition that wraps a potential temperature flux and converts it to
an energy flux for use with diagnostic energy density fields.

The materialized diagnostic uses the inverse physical energy-to-theta conversion.
For reference-pressure dynamics, ``𝒬 = Jᶿ cᵖᵐ Π``. Density-state dynamics
uses the same fixed-phase Jacobian as the forward conversion.
"""
function ThetaFluxBoundaryCondition(flux)
    tf = ThetaFluxBoundaryConditionFunction(flux, nothing, nothing, nothing)
    return BoundaryCondition(Flux(), tf)
end

#####
##### Shared helper: map a per-side BC function over all seven sides
#####

# Apply `f` to each of the seven `FieldBoundaryConditions` sides, threading any extra `args`
# through, and reconstruct a `FieldBoundaryConditions` from the results.
map_field_boundary_conditions(f, fbcs::FieldBoundaryConditions, args...) =
    FieldBoundaryConditions(; west     = f(fbcs.west, args...),
                              east     = f(fbcs.east, args...),
                              south    = f(fbcs.south, args...),
                              north    = f(fbcs.north, args...),
                              bottom   = f(fbcs.bottom, args...),
                              top      = f(fbcs.top, args...),
                              immersed = f(fbcs.immersed, args...))

#####
##### Conversion functions: energy ↔ theta boundary conditions
#####

# Convert ρs BCs → ρθ BCs (for LiquidIcePotentialTemperatureFormulation)
energy_to_theta_bc(bc) = bc
energy_to_theta_bc(bc::BulkSensibleHeatFluxBoundaryCondition) = bc
energy_to_theta_bc(bc::BoundaryCondition{<:Flux}) = EnergyFluxBoundaryCondition(bc.condition)

energy_to_theta_bcs(fbcs::FieldBoundaryConditions) = map_field_boundary_conditions(energy_to_theta_bc, fbcs)

# Convert ρθ BCs → ρs BCs (for diagnostic energy_density with PotentialTemperatureFormulation)
theta_to_energy_bc(bc) = bc
# For EnergyFluxBC, extract the original energy flux
theta_to_energy_bc(bc::EnergyFluxBCType) = BoundaryCondition(Flux(), bc.condition.condition)
# For regular flux BCs (actual θ fluxes), wrap to multiply by cᵖᵐ
theta_to_energy_bc(bc::BoundaryCondition{<:Flux}) = ThetaFluxBoundaryCondition(bc.condition)

theta_to_energy_bcs(fbcs::FieldBoundaryConditions) = map_field_boundary_conditions(theta_to_energy_bc, fbcs)

#####
##### Strip surface fluxes for adiabatic initialization
#####

# Replace any surface flux boundary condition with no-flux, passing every other (dynamical)
# boundary condition through unchanged. On a prognostic scalar every non-zero surface flux is a
# diabatic source, so a single `Flux` method covers the wrapped flux types (bulk sensible-heat,
# energy, θ, and vapor) and a plain `FluxBoundaryCondition` alike; an existing no-flux BC maps to
# an equivalent no-flux, and Value/Gradient/Periodic/Open BCs pass through the fallback.
adiabatic_scalar_bc(bc) = bc
adiabatic_scalar_bc(::BoundaryCondition{<:Flux}) = OceananigansBC.NoFluxBoundaryCondition()

AtmosphereModels.adiabatic_scalar_bcs(fbcs::FieldBoundaryConditions) =
    map_field_boundary_conditions(adiabatic_scalar_bc, fbcs)

#####
##### Regularization functions for BC wrappers
#####

# Regularize EnergyFluxBoundaryCondition: populate side, microphysics, and thermodynamic_constants
const UnregularizedEnergyFluxBC = BoundaryCondition{<:Flux, <:EnergyFluxBoundaryConditionFunction{<:Any, Nothing}}

function materialize_atmosphere_boundary_condition(bc::UnregularizedEnergyFluxBC,
                                                   side, loc, grid, dynamics, microphysics, surface_pressure, constants,
                                                   microphysical_fields, specific_prognostic_moisture, temperature)
    ef = bc.condition
    density = dynamics_density(dynamics)
    new_ef = EnergyFluxBoundaryConditionFunction(ef.condition, side, microphysics, constants, density, ef.response)
    return BoundaryCondition(Flux(), new_ef)
end

# Materialize ThetaFluxBoundaryCondition: populate side, microphysics, and thermodynamic_constants
const UnregularizedThetaFluxBC = BoundaryCondition{<:Flux, <:ThetaFluxBoundaryConditionFunction{<:Any, Nothing}}

function materialize_atmosphere_boundary_condition(bc::UnregularizedThetaFluxBC,
                                                   side, loc, grid, dynamics, microphysics, surface_pressure, constants,
                                                   microphysical_fields, specific_prognostic_moisture, temperature)
    tf = bc.condition
    density = dynamics_density(dynamics)
    new_tf = ThetaFluxBoundaryConditionFunction(tf.condition, side, microphysics, constants, density, tf.response)
    return BoundaryCondition(Flux(), new_tf)
end

#####
##### Set formulation on BulkSensibleHeatFlux for StaticEnergyFormulation
#####

set_sensible_heat_formulation(bc, formulation) = bc

function set_sensible_heat_formulation(bc::BulkSensibleHeatFluxBoundaryCondition, formulation)
    bf = bc.condition
    new_bf = BulkSensibleHeatFluxFunction(bf.side, bf.coefficient, bf.gustiness, bf.surface_temperature,
                                          bf.surface_pressure, bf.standard_pressure, bf.thermodynamic_constants,
                                          formulation, bf.filtered_velocities, bf.filtered_scalar)
    return BoundaryCondition(Flux(), new_bf)
end

set_sensible_heat_formulation_bcs(fbcs::FieldBoundaryConditions, formulation) =
    map_field_boundary_conditions(set_sensible_heat_formulation, fbcs, formulation)

# Capture formulation/solver metadata before the dynamics allocates prognostic fields.
set_energy_flux_response(bc, args...) = bc
function set_energy_flux_response(bc::EnergyFluxBCType, formulation, dynamics, grid, microphysics)
    ef = bc.condition
    response = AtmosphereModels.energy_flux_response(formulation, dynamics, grid, microphysics)
    return BoundaryCondition(Flux(), EnergyFluxBoundaryConditionFunction(ef.condition, ef.side,
        ef.microphysics, ef.thermodynamic_constants, ef.density, response))
end

function set_energy_flux_response(bc::ThetaFluxBCType, formulation, dynamics, grid, microphysics)
    tf = bc.condition
    response = AtmosphereModels.energy_flux_response(formulation, dynamics, grid, microphysics)
    return BoundaryCondition(Flux(), ThetaFluxBoundaryConditionFunction(tf.condition, tf.side,
        tf.microphysics, tf.thermodynamic_constants, tf.density, response))
end
